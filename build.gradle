/*
 * Copyright 2014 David W Millar
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.os.OperatingSystem

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'idea'

version       = '0.1.0'
description   = 'Client for GuerrillaMail Json API'
mainClassName = 'gm.client.Client'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
  groovyVersion   = '2.3.4'
  spockVersion    = '0.7-groovy-2.0'
  codenarcVersion = '0.21'
  jacksonVersion  = '2.4.1'
}

repositories {
  mavenCentral()
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1:'

  // Testing
  testCompile "org.spockframework:spock-core:${spockVersion}"
  compile 'co.freeside:betamax:1.1.2'
}

processResources {
  filter ReplaceTokens, tokens: [
      version     : version,
      buildDate   : new Date().format("yyyy-MM-dd HH:mm:ss.SSSZ"),
      username    : System.properties["user.name"],
      javaVersion : System.properties["java.version"],
      osName      : System.properties["os.name"],
      osVersion   : System.properties["os.version"]
    ]
}

codenarc {
  configFile             = file("${project.projectDir}/config/codenarc.groovy")
  ignoreFailures         = true
  reportFormat           = 'html'
  toolVersion            = codenarcVersion
  maxPriority1Violations = 0
  maxPriority2Violations = 5
  maxPriority3Violations = 10
}
