import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.os.OperatingSystem

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'jacoco'

version       = '0.1.0'
description   = 'Client for GuerrillaMail Json API'

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'check', 'jacocoTestReport'

ext {
  groovyVersion   = '2.3.4'
  spockVersion    = '0.7-groovy-2.0'
  codenarcVersion = '0.21'
  jacksonVersion  = '2.4.1'
}

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1:'

  // Testing
  testCompile "org.spockframework:spock-core:${spockVersion}"
  compile 'co.freeside:betamax:1.2-SNAPSHOT'
}

processResources {
  filter ReplaceTokens, tokens: [
      version     : version,
      buildDate   : new Date().format("yyyy-MM-dd HH:mm:ss.SSSZ"),
      username    : System.properties["user.name"],
      javaVersion : System.properties["java.version"],
      osName      : System.properties["os.name"],
      osVersion   : System.properties["os.version"]
    ]
}

codenarc {
  configFile             = file("${project.projectDir}/config/codenarc.groovy")
  ignoreFailures         = true
  reportFormat           = 'html'
  toolVersion            = codenarcVersion
  maxPriority1Violations = 0
  maxPriority2Violations = 5
  maxPriority3Violations = 10
}


