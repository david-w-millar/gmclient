/*
 * Copyright 2014 David W Millar
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.os.OperatingSystem

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'signing'
apply plugin: 'maven'

version       = '0.1.0'
description   = 'Client for GuerrillaMail Json API'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
  groovyVersion   = '2.3.4'
  spockVersion    = '0.7-groovy-2.0'
  codenarcVersion = '0.21'
  jacksonVersion  = '2.4.1'
  feignVersion    = '6.1.3'
}

repositories {
  mavenCentral()
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1:'

  // Feign
  compile "com.netflix.feign:feign-jackson:${feignVersion}"
  compile "com.netflix.feign:feign-jackson:${feignVersion}"


  // Testing
  testCompile "org.spockframework:spock-core:${spockVersion}"
  compile 'co.freeside:betamax:1.1.2'
}

processResources {
  filter ReplaceTokens, tokens: [
      version     : version,
      buildDate   : new Date().format("yyyy-MM-dd HH:mm:ss.SSSZ"),
      username    : System.properties["user.name"],
      javaVersion : System.properties["java.version"],
      osName      : System.properties["os.name"],
      osVersion   : System.properties["os.version"]
    ]
}

codenarc {
  configFile             = file("${project.projectDir}/config/codenarc.groovy")
  ignoreFailures         = true
  reportFormat           = 'html'
  toolVersion            = codenarcVersion
  maxPriority1Violations = 0
  maxPriority2Violations = 5
  maxPriority3Violations = 10
}


tasks.withType(Groovydoc) {
  link 'http://download.oracle.com/javase/6/docs/api/',
         'java.util',
         'java.lang'

  docTitle    = 'Groovy API Documentation for groovy-pluralize-en $version'
  windowTitle = 'Groovy API for groovy-pluralize-en $version'
  footer      = "<p style='text-align: center'>Groovy API Documentation for groovy-pluralize-en $version - Licensed under the Apache License, Version 2.0 - <#
}

// ----- [ Packaging and publishing ] -----

task sourceJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

artifacts {
  archives javadocJar
  archives sourceJar
  archives jar
}

uploadArchives { task ->
  task.onlyIf { isReleaseVersion || ! project.hasProperty('ci') }

  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(task)) {
          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }
          snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }
        }
      }

      pom.project {
        name "${project.name}:${project.version}"
        description "${project.description}"
        inceptionYear '2014'
        url project_url
        packaging 'jar'

        scm {
          connection 'git@github.com:david-w-millar/groovy-pluralize-en.git'
          developerConnection 'git@github.com:david-w-millar/groovy-pluralize-en.git'
          url project_url
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'david-w-millar'
            name 'David W Millar'
            email 'david.w.millar@gmail.com'
            organization project_url
            organizationUrl project_url
          }
        }

      }
    }
  }
}


